/***
File: CraftingSlot
Contributers: Myles Busig
Last Edited: 5/24/2018
Last Edited by: Myles Busig
Brief: This is one of the slots used to craft items
***/

class CraftingSlot : ZilchComponent
{
    var OverworldSpace : Space = null;
    
    var Data : Integer = 0;
    
    [Property] var MinTimeBetweenSwitch : Real = 0.5;
    var TimeTillCanSwitch : Real = 0.0;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, Events.MouseUpdate, this.OnMouseHover);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnMouseHover(event : ViewportMouseEvent)
    {
        if (Zero.Mouse.IsButtonDown(MouseButtons.Left) && this.TimeTillCanSwitch <= 0.0)
        {
            var selectedColor = this.OverworldSpace.LevelSettings.Crafting.SelectedColor;
            
            for (var i = 0; i < 10; ++i)
            {
                if (selectedColor == this.Space.FindObjectByName("CraftingColor`i + 1`").Sprite.VertexColor)
                {
                    if (i != 0)
                    {
                        if (this.OverworldSpace.LevelSettings.Crafting.ColorAmounts[i - 1] > 0)
                        {
                            if (this.Data != i)
                            {
                                if (this.Data > 0)
                                    ++this.OverworldSpace.LevelSettings.Crafting.ColorAmounts[this.Data - 1];
                                
                                this.Data = i;
                                --this.OverworldSpace.LevelSettings.Crafting.ColorAmounts[i - 1];
                                this.UpdateColor();
                            }
                        }
                    }
                    else
                    {
                        if (this.Data > 0)
                            ++this.OverworldSpace.LevelSettings.Crafting.ColorAmounts[this.Data - 1];
                        
                        this.Data = 0;
                        
                        this.UpdateColor();
                    }
                }
            }
            
            Console.WriteLine("Slot: `this.Owner.Name`, Data: `this.Data`");
            
            this.TimeTillCanSwitch = this.MinTimeBetweenSwitch;
        }
    }
    
    function UpdateColor()
    {
        this.Owner.Sprite.VertexColor = this.OverworldSpace.LevelSettings.Crafting.SelectedColor;
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        this.TimeTillCanSwitch -= event.Dt;
    }
}
